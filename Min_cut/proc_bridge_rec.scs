scp_program -> proc_bridge_rec (*

	-> rrel_params: ... (*

        	-> rrel_1: rrel_in: _unvisvert;;
        	-> rrel_2: rrel_in: _smezh;;
        	-> rrel_3: rrel_in: _edges;;
        	-> rrel_4: rrel_in: _v1;;
    *);;


    -> rrel_operators: ... (*

		-> rrel_init: ..proc_bridge_rec_search_smezh (*

            	<- searchElStr3;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _smezh;;
            	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
            	-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _vert1;;

            	=> nrel_then: ..proc_bridge_rec_er_from_smezh ;;
            	=> nrel_else: ..proc_bridge_rec_return;;
        	*);;


        	-> ..proc_bridge_rec_er_from_smezh (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc1;;

            	=> nrel_goto: ..proc_bridge_rec_search_unvisvert;;
        	*);;


        	-> ..proc_bridge_rec_search_unvisvert (*

            	<- searchElStr3;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _unvisvert;;
            	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1_1;;
            	-> rrel_3: rrel_fixed: rrel_scp_var: _vert1;;

            	=> nrel_then: ..proc_bridge_rec_search_arc;;
            	=> nrel_else: ..proc_bridge_rec_search_smezh;;
        	*);;


        	-> ..proc_bridge_rec_search_arc (*

            	<- searchElStr3;;
           	-> rrel_1: rrel_fixed: rrel_scp_var: _v1;;
           	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _inz_arc;;
           	-> rrel_3: rrel_fixed: rrel_scp_var: rrel_node: _vert1;;

           	=> nrel_then: ..proc_bridge_rec_search_edges;;
           	=> nrel_else: ..proc_bridge_rec_return;;
        	*);;


        	-> ..proc_bridge_rec_search_edges (*

            	<- searchElStr3;;
           	-> rrel_1: rrel_fixed: rrel_scp_var: _edges;;
           	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc0;;
          	-> rrel_3: rrel_fixed: rrel_scp_var: _inz_arc;;

           	=> nrel_then: ..proc_bridge_rec_er_from_unvisvert;;
           	=> nrel_else: ..proc_bridge_rec_search_smezh;;
        	*);;


        	-> ..proc_bridge_rec_er_from_unvisvert (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc1_1;;
 
            	=> nrel_goto: ..proc_bridge_rec_search_smezh_vert1;;
        	*);;


       	-> ..proc_bridge_rec_search_smezh_vert1 (*

            	<- searchSetStr3;;
           	-> rrel_1: rrel_fixed: rrel_scp_var: _vert1;;
           	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _inz_arc;;
           	-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _smezh_vert2;;

           	-> rrel_set_3: rrel_assign: rrel_scp_var: rrel_node: _smezh_v;;

           	=> nrel_then: ..proc_bridge_rec_call_bridge_rec;;
           	=> nrel_else: ..proc_bridge_rec_search_smezh;;
        	*);;


        	-> ..proc_bridge_rec_call_bridge_rec (*

            	<- call;;
            	-> rrel_1: rrel_fixed: rrel_scp_const: proc_bridge_rec;;
            	-> rrel_2: rrel_fixed: rrel_scp_const: ...
            	(*
                	-> rrel_1: rrel_fixed: rrel_scp_var: _unvisvert;;
                	-> rrel_2: rrel_fixed: rrel_scp_var: _smezh_v;;
                	-> rrel_3: rrel_fixed: rrel_scp_var: _edges;;
                	-> rrel_4: rrel_fixed: rrel_scp_var: _vert1;;
            	*);;
            	-> rrel_3: rrel_assign: rrel_scp_var: _descr3;;

           	=> nrel_goto: ..proc_bridge_rec_wait_bridge_rec;;
        	*);;


        	-> ..proc_bridge_rec_wait_bridge_rec (*

            	<- waitReturn;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _descr3;;

            	=> nrel_goto: ..proc_bridge_rec_search_smezh;;
        	*);;


        	-> ..proc_bridge_rec_return (*

            	<- return;;
        	*);;
    *);;
*);;
