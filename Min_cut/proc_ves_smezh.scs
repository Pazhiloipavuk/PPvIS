scp_program -> proc_ves_smezh (*
	
	-> rrel_params: ... (*

		-> rrel_1: rrel_in: _graph;;
	*);;

	-> rrel_operators: ... (*

		-> rrel_init: ..proc_ves_smezh_min_cut_initialize (*

				<- varAssign;;
				-> rrel_1: rrel_assign: rrel_scp_var: _min_cut;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [1000];;

				=> nrel_goto: ..proc_ves_smezh_search_vertex;;
			*);;


		-> ..proc_ves_smezh_search_vertex(*

				<- searchSetStr5;;
				-> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
				-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
				-> rrel_3: rrel_assign: rrel_scp_var: _el1;;
				-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
				-> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;
		
				->rrel_set_3: rrel_assign: rrel_scp_var: _not_checked_vertexes;;

				=> nrel_goto: ..proc_ves_smezh_vertex;;		
			*);;
		
	
		-> ..proc_ves_smezh_vertex (*

				<- searchElStr3;;
		 		-> rrel_1: rrel_fixed: rrel_scp_var: _not_checked_vertexes;;
				-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc3;;
				-> rrel_3: rrel_assign: rrel_scp_var: _el_print;;

			 	=> nrel_then: ..proc_ves_smezh_summ_initialize ;;	
				=> nrel_else: ..proc_ves_smezh_bridge_call;;
			*);;


		-> ..proc_ves_smezh_summ_initialize (*

				<- varAssign;;
				-> rrel_1: rrel_assign: rrel_scp_var: _summ;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [0];;

				=> nrel_goto: ..proc_ves_smezh_er_from_not_checked;;
			*);;


		-> ..proc_ves_smezh_er_from_not_checked (*

				<- eraseEl;;
		 		-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc3;;

		 		=> nrel_goto: ..proc_ves_smezh_search_smezh_vertex;;	
			*);;


		-> ..proc_ves_smezh_search_smezh_vertex(*

				<- searchSetStr3;;
		 		-> rrel_1: rrel_fixed: rrel_scp_var:_el_print;;
				-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc4;;
				-> rrel_3: rrel_assign: rrel_scp_var: _el_smezh;;
			
				->rrel_set_3: rrel_assign: rrel_scp_var: _set_smezh;;

				=>nrel_then: ..proc_ves_smezh_search_vertex_set_smezh;;
				=>nrel_else: ..proc_ves_smezh_vertex;;
			*);;


		-> ..proc_ves_smezh_search_vertex_set_smezh(*

				<- searchElStr3;;
		 		-> rrel_1: rrel_fixed: rrel_scp_var: _set_smezh;;
				-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc5;;
				-> rrel_3: rrel_assign: rrel_scp_var: _el_print_smezh;;

				=>nrel_then: ..proc_ves_smezh_search_smezh_is_vertex;;
				=>nrel_else: ..proc_ves_smezh_if_bolshe;;
			*);;	


		-> ..proc_ves_smezh_search_smezh_is_vertex(*

				<- searchElStr5;;
				-> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
				-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc10;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _el_print_smezh;;
				-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc20;;
				-> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;
	
				=> nrel_then: ..proc_ves_smezh_er_from_set_smezh;;	
				=> nrel_else: ..proc_ves_smezh_er;;	
			*);;


		-> ..proc_ves_smezh_er (*

				<- eraseEl;;
			 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc5;;

			 	=> nrel_goto: ..proc_ves_smezh_search_vertex_set_smezh;;	
			*);;


		-> ..proc_ves_smezh_er_from_set_smezh (*

				<- eraseEl;;
			 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc5;;
	
			 	=> nrel_goto: ..proc_ves_smezh_get_all_nodes_for_weigth;;	
			*);;


		-> ..proc_ves_smezh_get_all_nodes_for_weigth (*

				<- searchSetStr5;;
				-> rrel_1: rrel_fixed: rrel_scp_var: _el_print;;
				-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _neccesary_arc;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _el_print_smezh;;
				-> rrel_4: rrel_assign: rrel_scp_var: rrel_common: _arc8;;
				-> rrel_5: rrel_assign: rrel_scp_var: _weight;;
	
				->rrel_set_5: rrel_assign: rrel_scp_var: _all_nodes;;

				=> nrel_then: ..proc_ves_smezh_get_node;;
				=> nrel_else: ..proc_ves_smezh_return;;
			*);;


		-> ..proc_ves_smezh_get_node (*
	
				<- searchElStr3;;
				-> rrel_1: rrel_fixed: rrel_scp_var: _all_nodes;;
				-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _deleted_arc;;
				-> rrel_3: rrel_assign: rrel_scp_var: _weight;;

				=> nrel_then: ..proc_ves_smezh_delete_arc;; 
				=> nrel_else: ..proc_ves_smezh_return;;
			*);;


		-> ..proc_ves_smezh_delete_arc (*

				<- eraseEl;;
				-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _deleted_arc;;

				=> nrel_goto: ..proc_ves_smezh_if_weight;;
			*);;


		-> ..proc_ves_smezh_if_weight (*

				<- searchElStr5;;
				-> rrel_1: rrel_fixed: rrel_scp_var: _weight;;
				-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc1;;
				-> rrel_3: rrel_assign: rrel_scp_var: _weight_value;;
				-> rrel_4: rrel_assign: rrel_pos: rrel_scp_var: _arc2;;
				-> rrel_5: rrel_fixed: rrel_scp_const: nrel_system_identifier;;

				=> nrel_then: ..proc_ves_smezh_temp;;		
			 	=> nrel_else: ..proc_ves_smezh_get_node;;
			*);;


		-> ..proc_ves_smezh_temp (*

				<- varAssign;;
				-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _summ;;

				=> nrel_goto: ..proc_ves_smezh_get_sum;;
			*);;


		-> ..proc_ves_smezh_get_sum (*

				<- contAdd;;
				-> rrel_1: rrel_assign: rrel_scp_var: _summ;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _temp;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _weight_value;;

				=> nrel_goto: ..proc_ves_smezh_search_vertex_set_smezh;;
			*);;


		-> ..proc_ves_smezh_if_bolshe(*

				<- ifGr;;
				-> rrel_1: rrel_fixed: rrel_scp_var: _min_cut;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _summ;;

				=> nrel_then: ..proc_ves_smezh_min_cut_assign_summ;;
				=> nrel_else: ..proc_ves_smezh_vertex;;
			*);;


		-> ..proc_ves_smezh_min_cut_assign_summ (*

				<- varAssign;;
				-> rrel_1: rrel_assign: rrel_scp_var: _min_cut;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _summ;;

				=> nrel_goto: ..proc_ves_smezh_vertex;;
			*);;


		-> ..proc_ves_smezh_bridge_call (*

				<- call;;
				-> rrel_1: rrel_fixed: rrel_scp_const: proc_bridge;;
				-> rrel_2: rrel_fixed: rrel_scp_const: ...
				(*
                		-> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
               	 	-> rrel_2: rrel_fixed: rrel_scp_var: _min_cut;;
            		*);; 
            		-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

            		=> nrel_goto: ..proc_ves_smezh_bridge_wait;;
       		*);;


        	-> ..proc_ves_smezh_bridge_wait (*

           		<- waitReturn;;
            		-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

            		=> nrel_goto: ..proc_ves_smezh_er_set_not_checked;;
			*);;


		-> ..proc_ves_smezh_er_set_not_checked (*

				<- eraseEl;;
		 		-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _not_checked_vertexes;;

		 		=> nrel_goto: ..proc_ves_smezh_return;;
			*);;


		-> ..proc_ves_smezh_return (*

				<- return;;
			*);;
		*);;
*);;
