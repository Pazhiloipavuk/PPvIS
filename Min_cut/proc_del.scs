scp_program -> proc_del (*
	
	-> rrel_params: ... (*

		-> rrel_1: rrel_in: _graph;;
		-> rrel_2: rrel_in: _min_cut;;
    *);;

	-> rrel_operators: ... (*

		-> rrel_init: ..proc_del_initialize_best_cut (*

			<- varAssign;;
			-> rrel_1: rrel_assign: rrel_scp_var: _best_cut;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;

			=> nrel_goto: ..proc_del_initialize_best_min;;
		*);;


		-> ..proc_del_initialize_best_min (*

			<- varAssign;;
			-> rrel_1: rrel_assign: rrel_scp_var: _best_min;;
			-> rrel_2: rrel_fixed: rrel_scp_const: _min_cut;;

			=> nrel_goto: ..proc_del_search_arcs_unvis;;
		*);;

	
		-> ..proc_del_search_arcs_unvis (*

            	<- searchSetStr5;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: arc1;;
            	-> rrel_3: rrel_assign: rrel_scp_var: _graph_arc;;
            	-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
            	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_arc;;

	       	-> rrel_set_3: rrel_assign: rrel_scp_var: rrel_node: _unvisedges;;

            	=> nrel_then: ..proc_del_unvisedges;;
            	=> nrel_else: ..proc_del_return;;
        	*);;


        	-> ..proc_del_unvisedges (*

            	<- searchElStr3;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _unvisedges;;
            	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
            	-> rrel_3: rrel_assign: rrel_scp_var: _graph_arc1;;

            	=> nrel_then: ..proc_del_er_from_unvisedges;;
			=> nrel_else: ..proc_del_print_best_min;;
        	*);;


        	-> ..proc_del_er_from_unvisedges (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc1;;

           	 => nrel_goto: ..proc_del_search_arc_in_graph;;
        	*);;


        	-> ..proc_del_search_arc_in_graph (*

            	<- searchElStr3;;
            	-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: _v1;;
            	-> rrel_2: rrel_fixed: rrel_scp_var: _graph_arc1;;
            	-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _v2;;

            	=> nrel_then: ..proc_del_search_reverse_arc_in_graph;;
            	=> nrel_else: ..proc_del_return;;
        	*);;


        	-> ..proc_del_search_reverse_arc_in_graph (*

            	<- searchElStr3;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _v2;;
            	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _graph_arc2;;
            	-> rrel_3: rrel_fixed: rrel_scp_var: _v1;;

            	=> nrel_then: ..proc_del_search_reverse_arc_in_unvis;;
            	=> nrel_else: ..proc_del_return;;
       	 *);;


        	-> ..proc_del_search_reverse_arc_in_unvis (*

            	<- searchElStr3;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _unvisedges;;
            	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
           	-> rrel_3: rrel_fixed: rrel_scp_var: _graph_arc2;;

            	=> nrel_then: ..proc_del_er_reverse_from_unvisedges;;
            	=> nrel_else: ..proc_del_return;;
        	*);;


        	-> ..proc_del_er_reverse_from_unvisedges (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc1;;
           
            	=> nrel_goto: ..proc_del_get_all_nodes;;
        	*);;


		-> ..proc_del_get_all_nodes (*

			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _v1;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _neccesary_arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _v2;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_common: _arc144;;
			-> rrel_5: rrel_assign: rrel_scp_var: _weight;;

			->rrel_set_5: rrel_assign: rrel_scp_var: _all_nodes;;

			=> nrel_then: ..proc_del_get_node;;
			=> nrel_else: ..proc_del_return;;
		*);;


		-> ..proc_del_get_node (*

			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _all_nodes;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _deleted_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _weight;;

			=> nrel_then: ..proc_del_delete_arc;; 
			=> nrel_else: ..proc_del_return;;
		*);;


		-> ..proc_del_delete_arc (*

			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _deleted_arc;;

			=> nrel_goto: ..proc_del_if_weight;;
		*);;


		-> ..proc_del_if_weight(*

			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _weight;;
			-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _weight_value;;
			-> rrel_4: rrel_assign: rrel_pos: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_system_identifier;;

			=> nrel_then: ..proc_del_temp;;		
		 	=> nrel_else: ..proc_del_get_node;;
		*);;


		-> ..proc_del_temp (*

			<- varAssign;;
			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _best_cut;;

			=> nrel_goto: ..proc_del_summ_best_cut_weight;;
		*);;


		-> ..proc_del_summ_best_cut_weight (*

			<- contAdd;;
			-> rrel_1: rrel_assign: rrel_scp_var: _best_cut;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _temp;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _weight_value;;

			=> nrel_goto: ..proc_del_er_graph_arc1;;
		*);;


        	-> ..proc_del_er_graph_arc1 (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _graph_arc1;;
           
            	=> nrel_goto: ..proc_del_er_graph_arc2;;
        	*);;


        	-> ..proc_del_er_graph_arc2 (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _graph_arc2;;
           
            	=> nrel_goto: ..proc_del_search_smezh_v1;;
        	*);;


		-> ..proc_del_search_smezh_v1 (*

           	<- searchSetStr3;;
           	-> rrel_1: rrel_fixed: rrel_scp_var: _v1;;
           	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _inz_arc;;
           	-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _smez_vert1;;

           	=> nrel_then: ..proc_del_search_smezh_v2;;
           	=> nrel_else: ..proc_del_er_v1;;
        	*);;


        	-> ..proc_del_er_v1 (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _v1;;
           
            	=> nrel_goto: ..proc_del_search_smezh_v2;;
        	*);;


        	-> ..proc_del_search_smezh_v2 (*

           	<- searchSetStr3;;
           	-> rrel_1: rrel_fixed: rrel_scp_var: _v2;;
           	-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _inz_arc;;
           	-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _smez_vert1;;

           	=> nrel_then: ..proc_del_call_bridge_del;;
           	=> nrel_else: ..proc_del_er_v2;;
        	*);;


        	-> ..proc_del_er_v2 (*

            	<- eraseEl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _v2;;
           
            	=> nrel_goto: ..proc_del_call_bridge_del;;
        	*);;


		-> ..proc_del_call_bridge_del (*

			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_bridge_del;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... 
			(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _best_min;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _best_cut;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr7;;

			=> nrel_goto: ..proc_del_wait_bridge_del;;
		*);;

	
		-> ..proc_del_wait_bridge_del (*

			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr7;;

			=> nrel_goto: ..proc_del_unvisedges;;
		*);;


		-> ..proc_del_print_best_min (*

            	<- printNl;;
            	-> rrel_1: rrel_fixed: rrel_scp_var: _best_min;;

            	=> nrel_goto: ..proc_del_return;;
        	*);;


		-> ..proc_del_return (*

			<- return;;
		*);;
	*);;
*);;
